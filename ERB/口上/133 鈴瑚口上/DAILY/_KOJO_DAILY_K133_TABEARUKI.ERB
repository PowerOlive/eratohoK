;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K133_TABEARUKI_RATE(対象)
#DIM 対象
RETURN 40

;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K133_TABEARUKI_DECISION(対象)
#DIM 対象

SIF MASTER == NAME_TO_CHARA("サグメ")
	RETURN 0

;好感度500が必要
SIF CFLAG:対象:好感度 < 500
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0, 1)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K133_TABEARUKI_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K133_TABEARUKI(対象)
#DIM 対象

PRINTFORMW %ANAME(MASTER)%が仕事をしていると、%ANAME(対象)%が訪ねてきた
PRINTFORMW 「ねぇ%ANAME(MASTER)%、ご飯食べに行かない？」
PRINTFORML さて、どうしようか……
PRINTL
CALL ASK_YN("付き合う", "今は忙しい")

IF RESULT == 1
	PRINTFORMW 「ありゃっ、そうなの？」
	PRINTFORMW 「それなら仕方ないか、私一人でいこっかなー」
	PRINTFORMW %ANAME(対象)%は一人で出かけていった……
ELSE
	PRINTFORMW 「いいねいいね」
	PRINTFORMW 「じゃあ早速いこっか！　最近新しいお店ができてさー」
	PRINTFORMW %ANAME(対象)%の食べ歩きに付き合わされた……
	CALL COLOR_PRINTW("連れ回され、ちょっと体力がつきました", カラー_注意)
	CALL ADD_COOLTIME(MASTER, 1)
	CALL PRINT_ADD_EXP(MASTER, "武闘経験値", RAND(1, 3), 1)
	CALL TRAIN_AUTO_ABLUP(MASTER)
ENDIF

RETURN 1
