;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K60_WHOLE_ENERGY_RATE(対象)
#DIM 対象
RETURN 1000


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K60_WHOLE_ENERGY_DECISION(対象)
#DIM 対象

SIF KDVAR:対象:勇儀_四天王の本気
	RETURN 0

SIF GET_OWN_CITY(CFLAG:対象:所属) != 1
	RETURN 0

RETURN CHECK_KOJO_DAILY_HAPPEN(対象)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K60_WHOLE_ENERGY_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K60_WHOLE_ENERGY(対象)
#DIM 対象

PRINTFORMW 「あははは！　こりゃ歯応えが有る戦いだ。久々にわくわくしてきたよ！」
PRINTFORML %PRONOUN(対象)%の領地は残り僅かまでに追い詰められながら、それでも%ANAME(対象)%は快活に笑う
PRINTFORMW 絶体絶命の窮地であっても、彼女にとっては本気を出せる絶好の遊び場に過ぎなかった
PRINTFORML 「こっからは本気で相手させてもらうよ。さあ、思いっきりかかってきな！」

CALL COLOR_PRINTW(@"%ANAME(対象)%が本気を出しました", カラー_注意)
CALL COLOR_PRINTW(@"%ANAME(対象)%の能力が上昇しました", カラー_注意)
ABL:対象:武闘 += 5
ABL:対象:防衛 += 10
ABL:対象:知略 += 5

CALL SKILL_LEARN_BY_NAME(対象, スキル_ジャンル_PERSONAL, NO:対象, "三歩必殺")
SIF RESULT
	CALL COLOR_PRINTW(@"%ANAME(対象)%がスキル＜三歩必殺＞を開放しました", カラー_注意)

CALL SKILL_LEARN_BY_NAME(対象, スキル_ジャンル_TROOP, NO:対象, "怪力乱神")
SIF RESULT
	CALL COLOR_PRINTW(@"%ANAME(対象)%がスキル＜怪力乱神＞を開放しました", カラー_注意)

KDVAR:対象:勇儀_四天王の本気 = 1

RETURN 1

