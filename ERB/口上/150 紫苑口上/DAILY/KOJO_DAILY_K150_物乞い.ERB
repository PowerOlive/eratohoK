;---------------------
;基本的な発生確率(1000分率 100で10%)
;これにコンフィグ項目の発生頻度がかけられるので、必ずしもこの通りにはならない
;---------------------
@KOJO_DAILY_K150_GIVE_ME_MONEY_RATE(対象)
#DIM 対象
RETURN 80


;---------------------
;確率以外の発生判定
;〇期以降になると発生するとか、デイリー側で利用している変数が-1だったら起こさない　みたいなはじき方をするときに使う
;---------------------
@KOJO_DAILY_K150_GIVE_ME_MONEY_DECISION(対象)
#DIM 対象
SIF MONEY < 10000
	RETURN 0
RETURN CHECK_KOJO_DAILY_HAPPEN(対象, 1, 0, 1)

;---------------------
;ジャンル
;コンフィグ設定で使用
;---------------------
@KOJO_DAILY_K150_GIVE_ME_MONEY_GENRE(対象)
#DIM 対象
RETURN デイリー_ジャンル_その他

;---------------------
;本体
;イベントが発生した場合は1、発生しなかった場合は0を返す
;発生しなかったというのは例えば、特定条件を満たすキャラからランダムに一人選ぶデイリーで、そもそもその条件を満たすキャラが一人もいないみたいなとき
;旧仕様で作ったことある人向けにいうと「旧仕様のデイリー本体冒頭で-1を返すような状況」
;---------------------
@KOJO_DAILY_K150_GIVE_ME_MONEY(対象)
#DIM 対象
#DIM 金額
#DIM 上昇値
#DIM 能力値
金額 = MIN(IFRAND("1000, 5000, 10000, 30000, 50000", 1), MONEY)

PRINTFORMW 「%ANAME(MASTER)%、お金ちょうだい？」
PRINTFORML 仕事中、急に%ANAME(対象)%が訪ねてきたかと思ったら、茶碗を差し出してきた
PRINTFORMW どうやら物乞いにきたようだが……
CALL ICPRINT(@"……<{金額}>くらい欲しいかなぁ」", "W", カラー_注意)
PRINTFORMW なかなか強気の価格設定だ……
PRINTFORML 現在の所持金:{MONEY}

CALL ASK_YN("あげる", "あげない")

IF RESULT == 1
	PRINTFORMW 「けち！」
	PRINTFORML %ANAME(対象)%はむくれてしまった……
	RETURN 1
ENDIF

PRINTFORMW 「ほんとにくれるの？　やった！」
PRINTFORMW 「ありがとね！」
PRINTFORMW %ANAME(対象)%は恵比寿顔で立ち去っていった……

MONEY -= 金額
IF 金額 < 10000
	上昇値 = RAND:10000 < 金額
ELSE
	上昇値 = RAND:((金額 * 3 + 30000) / 20000)
ENDIF

SIF 上昇値 == 0
	RETURN 1

能力値 = IFRAND(@"{GETNUM(ABL, "武闘")}, {GETNUM(ABL, "防衛")}, {GETNUM(ABL, "知略")}, {GETNUM(ABL, "政治")}", 1)

CALL ICPRINT(@"やる気を出した%ANAME(対象)%の%ABLNAME:能力値%が<{上昇値}>上がった", "W", カラー_注意)

ABL:対象:能力値 += 上昇値

RETURN 1
