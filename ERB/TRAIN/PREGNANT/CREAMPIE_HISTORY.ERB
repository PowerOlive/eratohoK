;---------------------------------------------------------
;中出し履歴を更新する関数
;古くなった履歴を削除する
;---------------------------------------------------------
@UPDATE_CREAMPIE_HISTORY(ARG)
#DIM LCOUNT
FOR LCOUNT, 0, WOMB_RECORD_MAX
	SIF WOMB_SEMEN_ID:ARG:LCOUNT == 0
		CONTINUE
	;最終中出し日からの期間を加算
	WOMB_SEMEN_TIME:ARG:LCOUNT ++

	;回数が多ければ減っていく処理
	IF WOMB_SEMEN_COUNT:ARG:LCOUNT > 5
		LOCAL:1 = RAND:(WOMB_SEMEN_COUNT:ARG:LCOUNT / 3 + 1)
		WOMB_SEMEN_COUNT:ARG:LCOUNT -= LOCAL:1
		WOMB_SEMEN_AMOUNT:ARG:LCOUNT = MIN_DECREASE(WOMB_SEMEN_AMOUNT:ARG:LCOUNT, LOCAL:1 * 2, WOMB_SEMEN_COUNT:ARG:LCOUNT * 3 / 2)
	ENDIF

	;履歴が古ければリストから削除
	IF 	WOMB_SEMEN_TIME:ARG:LCOUNT >= 5
		WOMB_SEMEN_ID:ARG:LCOUNT = 0
		WOMB_SEMEN_AMOUNT:ARG:LCOUNT = 0
		WOMB_SEMEN_TIME:ARG:LCOUNT = 0
		WOMB_SEMEN_COUNT:ARG:LCOUNT = 0
	ENDIF
NEXT

;---------------------------------------------------------
;父親候補を履歴から探す関数
;妊娠している時点で父親はいるはずなので候補が0人である場合は一応エラーを投げて父親が不明ということにする
;RETURNF CASE
	;0:候補が複数など候補が1人でない場合
	;else:父親候補の番号
;---------------------------------------------------------
@GET_FATHER_CANDIDATE_FROM_CREAMPIE_HISTORY(ARG)
#FUNCTION
#DIM FATHER_CANDIDATE, WOMB_RECORD_MAX
#DIM SEMEN_AMOUNT_SUM
#DIM CANDIDATE_COUNT
#DIM CANDIDATE_CNO
#DIM CANDIDATE_VALUE
#DIM LCOUNT
VARSET CANDIDATE_COUNT
VARSET FATHER_CANDIDATE
VARSET SEMEN_AMOUNT_SUM

FOR LCOUNT, 0, WOMB_RECORD_MAX
	SIF WOMB_SEMEN_ID:ARG:LCOUNT == 0
		CONTINUE
	CANDIDATE_CNO = ID_TO_CHARA(WOMB_SEMEN_ID:ARG:LCOUNT)
	FATHER_CANDIDATE:CANDIDATE_COUNT = WOMB_SEMEN_ID:ARG:LCOUNT
	IF CANDIDATE_CNO >= 0
		SEMEN_AMOUNT_SUM += WOMB_SEMEN_AMOUNT:ARG:LCOUNT * (GETBIT(TALENT:CANDIDATE_CNO:淫乱系, 素質_淫乱_濃厚精液) + 1)
	ELSE
		SEMEN_AMOUNT_SUM += WOMB_SEMEN_AMOUNT:ARG:LCOUNT
	ENDIF
	CANDIDATE_COUNT ++
NEXT

SIF CANDIDATE_COUNT == 0
	CALLF ERROR

;父親が複数いる場合はランダムにする
;SIF CANDIDATE_COUNT != 1
;	RETURNF 0

IF CANDIDATE_COUNT == 1 || RAND:CANDIDATE_COUNT > 0
	CANDIDATE_VALUE = RAND:SEMEN_AMOUNT_SUM
	FOR LCOUNT, 0, WOMB_RECORD_MAX
		CANDIDATE_CNO = ID_TO_CHARA(WOMB_SEMEN_ID:ARG:LCOUNT)
		IF CANDIDATE_CNO >= 0
			CANDIDATE_VALUE -= WOMB_SEMEN_AMOUNT:ARG:LCOUNT * (GETBIT(TALENT:CANDIDATE_CNO:淫乱系, 素質_淫乱_濃厚精液) + 1)
		ELSE
			CANDIDATE_VALUE -= WOMB_SEMEN_AMOUNT:ARG:LCOUNT
		ENDIF
		SIF CANDIDATE_VALUE <= 0
			RETURNF WOMB_SEMEN_ID:ARG:LCOUNT
	NEXT
;父親候補が複数いる場合、(候補の数 - 1/候補の数)の確率で不明になる
ELSE
	RETURNF GET_SPERM_ID("不明")
ENDIF

;死に分岐
RETURNF GET_SPERM_ID("不明")

;---------------------------------------------------------
;中だし履歴を削除する関数
;---------------------------------------------------------
@CLEAR_CREAMPIE_HISTORY(ARG:0)
;中出し履歴をクリア
FOR LOCAL, 0, WOMB_RECORD_MAX
	WOMB_SEMEN_ID:(ARG:0):LOCAL = 0
	WOMB_SEMEN_AMOUNT:(ARG:0):LOCAL = 0
	WOMB_SEMEN_COUNT:(ARG:0):LOCAL = 0
	WOMB_SEMEN_TIME:(ARG:0):LOCAL = 0
NEXT
